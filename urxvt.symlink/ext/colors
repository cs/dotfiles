#!/usr/bin/env perl

use strict;
use warnings;

my %escapecodes = (
  "foreground"             => "10",
  "background"             => "11",
  "border"                 => "708",
  "cursorColor"            => "12",
  "pointerColorBackground" => "13",
  "pointerColorForeground" => "14"
);

for my $i (0..15) { $escapecodes{"color$i"} = "4;$i"; }

my @colors = keys(%escapecodes);

my @theme_sequence = ();
my $current_theme = 0; # 1st theme in sequence is the default
my %themes = ();

sub on_start {
  my ($self) = @_;

  @theme_sequence = split(/,/, $self->x_resource("%.themes"));

  foreach my $theme_id (@theme_sequence) {
    $themes{$theme_id} = ();
    for (@colors) {
      $themes{$theme_id}{$_} = $self->x_resource("%.$theme_id.$_");
    }
  }

  apply_current_theme();
}

sub on_action {
  my ($self, $action) = @_;

  if ($action eq "cycle") {
    $current_theme++;
    if ($current_theme > $#theme_sequence) { $current_theme = 0; } # wheel
    apply_current_theme();
  }
}

sub apply_current_theme {
  my $cmd = "";
  my $theme_id = $theme_sequence[$current_theme];

  for (@colors) {
    $cmd .= "\033]" . $escapecodes{$_} . ";";
    $cmd .= $themes{$theme_id}{$_} . "\033\\";
  }

  foreach my $term (urxvt::termlist) { $term->cmd_parse($cmd); }
}
