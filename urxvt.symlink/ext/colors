#!/usr/bin/env perl

use strict;
use warnings;

my %escapecodes = (
  "foreground"             => "10",
  "background"             => "11",
  "border"                 => "708",
  "cursorColor"            => "12",
  "pointerColorBackground" => "13",
  "pointerColorForeground" => "14"
);

for my $i (0..15) { $escapecodes{"color$i"} = "4;$i"; }

my @colors = keys(%escapecodes);

my %solarizedLight = ();
my %solarizedDark = ();

my $currentTheme = "undef";

sub on_start {
  my ($self) = @_;

  for (@colors) {
    $solarizedDark{$_} = $self->x_resource("%.solarized-dark.$_");
    $solarizedLight{$_} = $self->x_resource("%.solarized-light.$_");
  }

  if ($currentTheme eq "undef") {
    $currentTheme = "solarized-dark"; # default
  }

  apply_current_theme();
}

sub on_action {
  my ($self, $action) = @_;

  if ($action eq "cycle") {
    if ($currentTheme eq "solarized-dark") { $currentTheme = "solarized-light"; }
    else { $currentTheme = "solarized-dark"; }
    apply_current_theme();
  } elsif ($action eq "dark") {
    $currentTheme = "solarized-dark";
    apply_current_theme();
  } elsif ($action eq "light") {
    $currentTheme = "solarized-light";
    apply_current_theme();
  }
}

sub apply_current_theme {
  my $cmd = "";
  my %theme = ();

  if ($currentTheme eq "solarized-dark") { %theme = %solarizedDark; }
  elsif ($currentTheme eq "solarized-light") { %theme = %solarizedLight; }

  for (@colors) {
    $cmd .= "\033]" . $escapecodes{$_} . ";" . $theme{$_} . "\033\\";
  }

  foreach my $term (urxvt::termlist) { $term->cmd_parse($cmd); }
}
